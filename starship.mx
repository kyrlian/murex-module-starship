
export STARSHIP_SHELL="murex"
rand int -> :str: left 16 -> export STARSHIP_SESSION_KEY

config set shell prompt {

    # Get the command status
    exitnum -> set STARSHIP_CMD_STATUS

    # Prepare the timer data, if needed.
    if { $STARSHIP_START_TIME } then {
        datetime --in "{now}" --out "{unix}" -> cast int -> set END_TIME
        out $STARSHIP_START_TIME -> cast int -> set START_TIME
        $END_TIME - $START_TIME -> set DURATION
      
        out ${starship prompt --terminal-width=$ENV.COLS -s $STARSHIP_CMD_STATUS -j $ENV.NUM_JOBS -d $DURATION}
        unset STARSHIP_START_TIME
    } else {
        out ${starship prompt --terminal-width=$ENV.COLS -s $STARSHIP_CMD_STATUS -j $ENV.NUM_JOBS}
    }
}

event onPrompt starship=before {

    # how many bg jobs do we have
    fid-list --background -> count-> export NUM_JOBS

    # if $COLUMNS not defined
    if { $ENV.COLUMNS > 0 } then {
        export COLS = $ENV.COLUMNS
    } else { 
        try {
            tput cols -> export COLS
        }
        catch {
            export COLS = 128
        }
    }
}

event onPrompt starship=after {
    datetime --in "{now}" --out "{unix}" -> cast int -> export STARSHIP_START_TIME
    
}
